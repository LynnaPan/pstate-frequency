## Maintainer: pyamsoft <pyam.soft@gmail.com>

pkgname=pstate-frequency-git
pkgdesc="Easily control Intel p-state driver"
pkgver=1.0.5.git
pkgrel=1
arch=('i686' 'x86_64')
makedepends=('git')
optdepends=(
	# 'msr-tools: Allow acpi-cpufreq setting of Turbo Boost bit on Sandy Bride+ CPUs'
)
provides=('pstate-frequency')
conflicts=('pstate-frequency')
license=('GPLv2')
url="https://github.com/pyamsoft/pstate-frequency.git"
sha256sums=('SKIP') ## The SHA256 is constantly changing since this is
                    ## pulled from git so skip the verification check

## The name of the git project
_gitname=pstate-frequency

## The 'dev' branch is not guaranteed to be stable, and
## may enter periods where it fails to run, or possibly
## even fail to compile. While newer features will enter
## 'dev' before 'master' it is not recommended to use
## the dev branch on production machines.
_gitbranch=master
# _gitbranch=dev

source=("${_gitname}::git+${url}#branch=${_gitbranch}")

###############################################################################

## Please read config.mk for an explanation
## of the variable options in this file

## DEFAULTS
## _CXX="c++"
## _PREFIX="/usr/local"
## _ETCDIR="/etc"
## _INCLUDE_BASH_COMPLETION=1
## _INCLUDE_ZSH_COMPLETION=1
## _INCLUDE_UDEV_RULE=1

_CXX="c++"
_PREFIX="/usr"
_ETCDIR="/etc"
_INCLUDE_BASH_COMPLETION=1
_INCLUDE_ZSH_COMPLETION=1
_INCLUDE_UDEV_RULE=1

pkgver() {
	cd "$srcdir/$_gitname"
	echo "$(head -n 2 config.mk | tail -n 1 | cut -c10-).git"
}

build() {
	cd "$srcdir/$_gitname"
	make \
		CC="${_CC}" \
		INCLUDE_UDEV_RULE=${_INCLUDE_UDEV_RULE}
}

package() {
	cd "$srcdir/$_gitname"
	make \
		DESTDIR="$pkgdir" \
		CC="${_CC}" \
		PREFIX="${_PREFIX}" \
		ETCDIR="${_ETCDIR}" \
		INCLUDE_BASH_COMPLETION=${_INCLUDE_BASH_COMPLETION} \
		INCLUDE_ZSH_COMPLETION=${_INCLUDE_ZSH_COMPLETION} \
		INCLUDE_UDEV_RULE=${_INCLUDE_UDEV_RULE} \
		install
}
